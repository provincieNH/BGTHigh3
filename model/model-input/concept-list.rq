PREFIX : <https://register.geostandaarden.nl/informatiemodel/imgeo/2.1.1#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX citygml: <https://register.geostandaarden.nl/informatiemodel/imgeo-citygml/2.1.1#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX imgeo: <https://definities.geostandaarden.nl/def/imgeo#>
PREFIX dct: <http://purl.org/dc/terms/>
select ?classlabel ?schemelabel ?collectionlabel ?lowerconcept ?lowerconceptlabel ?broader ?lowerconceptbroaderlabel ?topconceptlabel
where {
#    bind (:BegroeidTerreindeel as ?class)
    ?class a owl:Class ;
           rdfs:subClassOf ?l1 .
    ?l1 owl:onProperty ?prop  ;
        owl:allValuesFrom/owl:oneOf/rdf:rest*/rdf:first ?topconcept .
    filter( ?prop in (:bgt-status, citygml:function))
    ?lowerconcept skos:broader* ?topconcept .
    ?lowerconcept skos:prefLabel ?lclabel ;
                skos:definition ?definition ;
                skos:inScheme ?scheme ;
    bind(strafter(str(?class), '#') as ?classlabel)
    bind(strafter(str(?scheme), '#') as ?collectionlabel)
    bind(replace(?collectionlabel, 'Plus', '') as ?schemelabel)
    bind(str(?lclabel) as ?lowerconceptlabel)
    optional {
        ?lowerconcept skos:broader ?broader .
        ?broader skos:prefLabel ?cbrlabel .
        bind (str(?cbrlabel) as ?lowerconceptbroaderlabel )
    }
    
    bind(if (bound(?cbrlabel), '' , ?schemelabel) as ?topconceptlabel)
}

